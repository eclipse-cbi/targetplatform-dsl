/*******************************************************************************
 * Copyright (c) 2012-2014 Obeo and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 * 
 * Contributors:
 *     Mikael Barbero (Obeo) - initial API and implementation
 *     Denis Golovin - Fix version qualifier parsing problems
 *******************************************************************************/
grammar org.eclipse.cbi.targetplatform.TargetPlatform with org.eclipse.xtext.common.Terminals

import "org.eclipse.cbi.targetplatform.model"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
											
TargetPlatform:
	(
		'target' name=STRING
		contents+=TargetContent*
	)?
;

TargetContent: 
	Options | Environment | IncludeDeclaration | Location
;

Options: 
	'with' options+=Option (','? options+=Option)*
;

Environment:
	{Environment}
	'environment' env+=ID (','? env+=ID)*
;

IncludeDeclaration:
	'include' importURI=STRING
;

Location:
	'location' ((ID=ID)? & uri=STRING) ('{'
		('with' options+=Option (','? options+=Option)*)?
		ius+=IU*
	'}')?
;

enum Option:
	INCLUDE_REQUIRED |
	INCLUDE_ALL_ENVIRONMENTS |
	INCLUDE_SOURCE |
	INCLUDE_CONFIGURE_PHASE
;

IU:
	ID=ID ( (';' 'version' '=')? (version=VersionRange|version=STRING) )? 
;

Version hidden():
	INT ('.' INT ('.' INT ('.' (ID|INT|QUALIFIER))?)?)?
;

VersionRange hidden (WS):
	( ('('|'[') Version ',' Version (')'|']') ) | Version | 'lazy'
;

@Override 
terminal INT returns ecore::EInt: ('0'..'9')+;
@Override 
terminal ID: '^'?('a'..'z'|'A'..'Z'|'_') ('.'? ('a'..'z'|'A'..'Z'|'^'|'_'|'-'|'0'..'9'))*;
terminal QUALIFIER: ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*;
